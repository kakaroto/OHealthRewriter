import java.util.Properties

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

// Load signing properties
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystorePropertiesFile.withInputStream { keystoreProperties.load(it) }
}

android {
    namespace "ca.kakaroto.ohealthrewriter"
    compileSdk 35

    defaultConfig {
        applicationId "ca.kakaroto.ohealthrewriter"
        minSdk 26
        targetSdk 34
        versionCode 1
        versionName "1.1"
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
        debug {
            // This ensures the debug build is signed with the default debug key.
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.13.1"
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "androidx.work:work-runtime-ktx:2.9.0"
    implementation "androidx.health.connect:connect-client:1.1.0-alpha10"
    implementation "androidx.recyclerview:recyclerview:1.3.2"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    testImplementation "junit:junit:4.13.2"
    testImplementation "org.mockito:mockito-core:5.12.0"
    testImplementation "org.mockito.kotlin:mockito-kotlin:5.2.1"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.8.1"

    // Robolectric
    testImplementation "org.robolectric:robolectric:4.12.1"
    // WorkManager Testing
    testImplementation "androidx.work:work-testing:2.9.0"

    // AndroidX Test Core
    testImplementation "androidx.test:core:1.5.0"
}
